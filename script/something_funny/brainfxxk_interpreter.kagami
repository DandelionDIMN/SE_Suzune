program = '++++++++++[>+++++++>++++++++++>+++>+<<<<-]>++.>+.+++++++..+++.>++.<<+++++++++++++++.>.+++.------.--------.>+.>.'
mem = array(256, 0)
size = program.size()
code = 0
idx = 0
ptr = 0
panic = false

fn OutOfRangeError()
  println('pointer out of range')
end

fn LeftBracketError()
  println('left bracket not found')
end

fn RightBracketError()
  println('right bracket not found')
end

fn JumpForward()
  local reg = 0
  local code = 0
  if mem[ptr] == 0
    idx = idx + 1
    
    while idx < size
      code = program[idx]
      
      if code == '['
        reg = reg + 1 
      elif code == ']' 
        if reg > 0; reg = reg - 1
        else; break; end
      end
      
      idx = idx + 1
    end
    
    if reg > 0
      RightBracketError()
      panic = true
    end
  end
end

fn JumpBackward()
  local reg = 0
  local code = 0
  
  if mem[ptr] != 0
    idx = idx - 1
    
    while idx > 0
      code = program[idx]
      
      if code == ']' 
        reg = reg + 1
      elif code == '['
        if reg > 0; reg = reg - 1
        else; break; end
      end
      
      idx = idx - 1
    end
    
    if program[idx] == '[' && reg > 0
      reg = reg - 1
    end
    
    if reg > 0
      LeftBracketError()
      panic = true
    end
  end
end

println('program size:' + size)

while idx < size
  if panic; break; end
  
  code = program[idx]
  
  if code == '>'
    if ptr < 255
    ptr = ptr + 1
    else
      OutOfRangeError()
      panic = true
    end
  elif code == '<'
    if ptr > 0
      ptr = ptr - 1
    else
      OutOfRangeError()
      panic = true
    end
  elif code == '+'; mem[ptr] = mem[ptr] + 1
  elif code == '-'; mem[ptr] = mem[ptr] - 1
  elif code == '.'; print(int2str(mem[ptr]))
  elif code == ','; mem[ptr] = str2int(getchar())
  elif code == '['; JumpForward()
  elif code == ']'; JumpBackward()
  end
  
  idx = idx + 1
end

print('\n')
